set(CPACK_PACKAGE_NAME "${PROJECT_NAME}")
set(CPACK_PACKAGE_VENDOR "Steffen Andr√© Langnes")
set(CPACK_PACKAGE_VERSION "${PROJECT_VERSION}")
set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "${PROJECT_DESCRIPTION}")
set(CPACK_PACKAGE_CONTACT "${CPACK_PACKAGE_VENDOR}")
set(CPACK_PACKAGE_INSTALL_DIRECTORY "${CPACK_PACKAGE_NAME}")
set(CPACK_PACKAGE_CHECKSUM SHA256)
set(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_LIST_DIR}/../LICENSE")
set(CPACK_VERBATIM_VARIABLES TRUE)
set(CPACK_ARCHIVE_COMPONENT_INSTALL ON)
set(CPACK_OUTPUT_FILE_PREFIX dist)

string(REGEX REPLACE "(.)" "\\\\\\1" ESCAPED_BINARY_DIR "${CMAKE_BINARY_DIR}")
set(CPACK_SOURCE_IGNORE_FILES
    "\\.git/"
    "${ESCAPED_BINARY_DIR}/"
)

if(CMAKE_SYSTEM_NAME STREQUAL Windows)
    set(CPACK_GENERATOR ZIP)
else()
    set(CPACK_GENERATOR TGZ)
endif()

set(CPACK_SOURCE_GENERATOR TGZ ZIP)

string(TOLOWER "${CMAKE_SYSTEM_NAME}" CMAKE_SYSTEM_NAME_LC)
string(TOLOWER "${CMAKE_SYSTEM_PROCESSOR}" CMAKE_SYSTEM_PROCESSOR_LC)
string(TOLOWER "${CMAKE_CXX_COMPILER_ID}" CMAKE_CXX_COMPILER_ID_LC)

if(MSVC AND DEFINED MSVC_TOOLSET_VERSION)
    set(COMPILER_VERSION "${MSVC_TOOLSET_VERSION}")
else()
    string(REPLACE "." ";" COMPILER_VERSION_LIST "${CMAKE_CXX_COMPILER_VERSION}")
    list(GET COMPILER_VERSION_LIST 0 COMPILER_VERSION_MAJOR)
    list(GET COMPILER_VERSION_LIST 1 COMPILER_VERSION_MINOR)
    if(COMPILER_VERSION_MINOR GREATER 0)
        set(COMPILER_VERSION "${COMPILER_VERSION_MAJOR}.${COMPILER_VERSION_MINOR}")
    else()
        set(COMPILER_VERSION "${COMPILER_VERSION_MAJOR}")
    endif()
endif()

set(COMPILER "${CMAKE_CXX_COMPILER_ID_LC}-${COMPILER_VERSION}")

if(APPLE AND CMAKE_OSX_ARCHITECTURES MATCHES ";")
    set(PACKAGE_NAME_ARCH universal)
else()
    set(PACKAGE_NAME_ARCH ${CMAKE_SYSTEM_PROCESSOR_LC})
endif()

if(CMAKE_CXX_STANDARD)
    set(CXX_STANDARD "cxx${CMAKE_CXX_STANDARD}")
endif()

# Package file names
set(CPACK_SOURCE_PACKAGE_FILE_NAME "${CPACK_PACKAGE_NAME}-${CPACK_PACKAGE_VERSION}-src")
set(CPACK_ARCHIVE_RUNTIME_FILE_NAME "${CPACK_PACKAGE_NAME}-${CPACK_PACKAGE_VERSION}-bin-${CMAKE_SYSTEM_NAME_LC}-${PACKAGE_NAME_ARCH}-${COMPILER}-${CXX_STANDARD}")
set(CPACK_ARCHIVE_DEVELOPMENT_PLATFORM_DEPENDENT_FILE_NAME "${CPACK_PACKAGE_NAME}-${CPACK_PACKAGE_VERSION}-dev-${CMAKE_SYSTEM_NAME_LC}-${PACKAGE_NAME_ARCH}-${COMPILER}-${CXX_STANDARD}")
set(CPACK_ARCHIVE_DEVELOPMENT_CROSS_PLATFORM_FILE_NAME "${CPACK_PACKAGE_NAME}-${CPACK_PACKAGE_VERSION}-dev")

# Specify components to package
set(CPACK_COMPONENTS_ALL)

if(LANGNES_JSON_PACKAGE_RUNTIME)
    list(APPEND CPACK_COMPONENTS_ALL langnes_json_library_runtime)
endif()

if(LANGNES_JSON_PACKAGE_LINK)
    list(APPEND CPACK_COMPONENTS_ALL langnes_json_library_link)
endif()

if(LANGNES_JSON_PACKAGE_HEADERS)
    list(APPEND CPACK_COMPONENTS_ALL langnes_json_library_headers)
endif()

if(LANGNES_JSON_PACKAGE_DOCS)
    list(APPEND CPACK_COMPONENTS_ALL langnes_json_library_docs)
endif()

if(LANGNES_JSON_PACKAGE_CMAKE)
    list(APPEND CPACK_COMPONENTS_ALL langnes_json_library_cmake)
endif()

# Component groups
set(CPACK_COMPONENT_LANGNES_JSON_LIBRARY_RUNTIME_GROUP runtime)
set(CPACK_COMPONENT_LANGNES_JSON_LIBRARY_LINK_GROUP development_platform_dependent)
set(CPACK_COMPONENT_LANGNES_JSON_LIBRARY_HEADERS_GROUP development_cross_platform)
set(CPACK_COMPONENT_LANGNES_JSON_LIBRARY_DOCS_GROUP development_cross_platform)
set(CPACK_COMPONENT_LANGNES_JSON_LIBRARY_CMAKE_GROUP development_platform_dependent)

include(CPack)
